"""
    Disjunction <: Property

Type that represents a disjunction of properties.

### Fields

- `disjuncts` -- vector of properties (elements are reordered by this type)
- `reorder`   -- flag to indicate whether shuffling is allowed

### Notes

The following formula characterizes whether a set ``X`` satisfies a disjunction
``ğ‘ƒ = ğ‘ƒ_1 âˆ¨ ğ‘ƒ_2 âˆ¨ â€¦ âˆ¨ ğ‘ƒ_m``:

```math
    X \\models ğ‘ƒ \\iff X \\models ğ‘ƒ_j \\text{ for some } 1 â‰¤ j â‰¤ m
```

If the `reorder` flag is set, the disjuncts may be reordered after each call to
[`check`](@ref check(ğ‘ƒ::Disjunction, X::LazySet{N}) where {N<:Real}) as a
heuristics to make subsequent checks faster.
"""
struct Disjunction <: Property
    disjuncts::Vector{Property}
    reorder::Bool
end

# default constructor with activated reordering
Disjunction(disjuncts::Vector{<:Property}) = Disjunction(disjuncts, true)

"""
    dim(ğ‘ƒ::Disjunction)::Int

Return the dimension of a disjunction of properties.

### Input

- `ğ‘ƒ` -- disjunction of properties

### Output

The dimension of the disjunction of properties.
"""
function dim(ğ‘ƒ::Disjunction)::Int
    if isempty(ğ‘ƒ.disjuncts)
        throw(ArgumentError("the dimension of an empty disjunction of " *
                            "properties is undefined"))
    end
    @inbounds return dim(ğ‘ƒ.disjuncts[1])
end

"""
    check(ğ‘ƒ::Disjunction, X::LazySet{N}; witness::Bool=false) where {N<:Real}

Check whether a convex set satisfies a disjunction of properties.

### Input

- `ğ‘ƒ`       -- disjunction of properties
- `X`       -- convex set
- `witness` -- (optional, default: `false`) flag for returning a counterexample
               if the property is violated

### Output

* If `witness` option is deactivated: `true` iff `X` satisfies the property `ğ‘ƒ`
* If `witness` option is activated:
  * `(true, [])` iff `X` satisfies the property `ğ‘ƒ`
  * `(false, v)` iff `X` does not satisfy the property `ğ‘ƒ` with witness `v`;
    note that `v == N[]` if ğ‘ƒ is the empty disjunction

### Notes

By convention, the empty disjunction is equivalent to `false` and hence is
satisfied by no set.

If the `ğ‘ƒ.reorder` flag is set, the disjuncts may be reordered as a heuristics
to make subsequent checks faster.
Since we check satisfaction from left to right, we move the disjunct for which
satisfaction was established to the front.

To be consistent with other propertes, the `witness` option only returns *one*
counterexample, namely for the left-most disjunct in the `disjuncts` vector.
We deactivate witness production for checking the remaining disjuncts.
"""
function check(ğ‘ƒ::Disjunction, X::LazySet{N};
               witness::Bool=false) where {N<:Real}
    v = N[]
    create_witness = witness
    for (i, conjunct) in enumerate(ğ‘ƒ.disjuncts)
        result = check(conjunct, X; witness=create_witness)
        if (create_witness && result[1]) || result
            _reorder!(ğ‘ƒ, i)
            return witness ? (true, N[]) : true
        elseif create_witness
            v = result[2]
            # deactivate witness production for remaining checks
            create_witness = false
        end
    end
    return witness ? (false, v) : false
end

function _reorder!(ğ‘ƒ::Disjunction, i::Int)
    if !ğ‘ƒ.reorder || i == 1
        return nothing
    end
    first = ğ‘ƒ.disjuncts[i]
    while i > 1
        ğ‘ƒ.disjuncts[i] = ğ‘ƒ.disjuncts[i-1]
        i -= 1
    end
    ğ‘ƒ.disjuncts[1] = first
    return nothing
end

@inline function project(ğ‘ƒ::Disjunction, vars::AbstractVector{Int})
    return Disjunction([project(disjunct, vars) for disjunct in ğ‘ƒ.disjuncts])
end
