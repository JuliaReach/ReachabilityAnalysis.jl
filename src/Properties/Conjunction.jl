"""
    Conjunction <: Property

Type that represents a conjunction of properties.

### Fields

- `conjuncts` -- vector of properties

### Notes

The following formula characterizes whether a set ``X`` satisfies a disjunction
``ð‘ƒ = ð‘ƒ_1 âˆ§ ð‘ƒ_2 âˆ§ â€¦ âˆ§ ð‘ƒ_m``:

```math
    X \\models ð‘ƒ \\iff X \\models ð‘ƒ_j \\text{ for all } 1 â‰¤ j â‰¤ m
```
"""
struct Conjunction <: Property
    conjuncts::Vector{Property}
end

"""
    dim(ð‘ƒ::Conjunction)::Int

Return the dimension of a conjunction of properties.

### Input

- `ð‘ƒ` -- conjunction of properties

### Output

The dimension of the conjunction of properties.
"""
function dim(ð‘ƒ::Conjunction)::Int
    if isempty(ð‘ƒ.conjuncts)
        throw(ArgumentError("the dimension of an empty conjunction of " *
                            "properties is undefined"))
    end
    @inbounds return dim(ð‘ƒ.conjuncts[1])
end

"""
    check(ð‘ƒ::Conjunction, X::LazySet{N}; witness::Bool=false) where {N<:Real}

Check whether a convex set satisfies a conjunction of properties.

### Input

- `ð‘ƒ`       -- conjunction of properties
- `X`       -- convex set
- `witness` -- (optional, default: `false`) flag for returning a counterexample
               if the property is violated

### Output

* If `witness` option is deactivated: `true` iff `X` satisfies the property `ð‘ƒ`
* If `witness` option is activated:
  * `(true, [])` iff `X` satisfies the property `ð‘ƒ`
  * `(false, v)` iff `X` does not satisfy the property `ð‘ƒ` with witness `v`

### Notes

By convention, the empty conjunction is equivalent to `true` and hence is
satisfied by any set.
"""
function check(ð‘ƒ::Conjunction, X::LazySet{N};
               witness::Bool=false) where {N<:Real}
    for conjunct in ð‘ƒ.conjuncts
        result = check(conjunct, X; witness=witness)
        if (witness && !result[1]) || !result
            return result
        end
    end
    return witness ? (true, N[]) : true
end

@inline function project(ð‘ƒ::Conjunction, vars::AbstractVector{Int})
    return Conjunction([project(conjunct, vars) for conjunct in ð‘ƒ.conjuncts])
end
